buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.2.3'
		classpath 'com.noveogroup.android:check:1.2.3'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'

// enable verbose lint warnings
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
}

dependencies {
	compile 'com.commonsware.cwac:wakeful:1.1.0'
}

def getVersionCode = {
	// Use `1` for the version code until we have a public release.  At that
	// stage, we'll have to start incrementing this for every release.
	return 1;
}
def getVersionName = {
	System.env.VERSION_TO_BUILD ?: 'SNAPSHOT'
}

android {
	compileSdkVersion 22
	buildToolsVersion '22.0.1'
	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	defaultConfig {
		versionCode getVersionCode()
		versionName getVersionName()
		archivesBaseName = "${project.name}-${versionName}"
	}

	applicationVariants.all {
		buildConfigField "String", "EXAMPLE_ENV_VAR", "\"${System.env.EXAMPLE}\"";
		buildConfigField "boolean", "DISABLE_APP_URL_VALIDATION", "Boolean.parseBoolean(\"${System.env.DISABLE_APP_URL_VALIDATION}\")";
		buildConfigField "String", "LOG_TAG", '"MedicGateway"'

	}

	signingConfigs {
		release {
			storeFile file(System.env.ANDROID_KEYSTORE_PATH ?: signingConfigs.debug.storeFile)
			storePassword System.env.ANDROID_KEYSTORE_PASSWORD ?: signingConfigs.debug.storePassword
			keyAlias System.env.ANDROID_KEY_ALIAS ?: signingConfigs.debug.keyAlias
			keyPassword System.env.ANDROID_KEY_PASSWORD ?: signingConfigs.debug.keyPassword
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFile getDefaultProguardFile('proguard-android.txt')
			signingConfig signingConfigs.release
		}
	}

	check {
		abortOnError true
	}

	lintOptions {
		disable 'UnusedResources' // linter can't handle static imports, so just skip this test
		disable 'IconMissingDensityFolder' // we don't care about nice icons at the moment
	}
}
